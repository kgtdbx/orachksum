SET SERVEROUTPUT ON FORMAT WRAPPED
SET FEEDBACK OFF
SET TRIM ON
SET TRIMSPOOL ON

DECLARE
  V_ORA_VER_MAJOR NUMBER;
  V_ORA_VERSION   VARCHAR2(20);
BEGIN
  select substr(version,1,instr(version,'.',1,4)-1),substr(version,1,instr(version,'.',1,1)-1) into v_ora_version,v_ora_ver_major from sys.v$instance;
  IF v_ora_version IN ('12.1.0.1','12.1.0.2') THEN
    execute immediate 'alter session set exclude_seed_cdb_view=false';
  ELSIF v_ora_ver_major >= 12 THEN
    execute immediate 'alter session set "_exclude_seed_cdb_view"=false';
  END IF;
END;
/

SPOOL &1
DECLARE
  V_ENCLOSURE VARCHAR2(1) := '"';
  V_SEPARATOR VARCHAR2(1) := ',';
  V_ORA_VER_MAJOR NUMBER;
  V_ORA_VERSION VARCHAR2(20);

  V_SQL CLOB;
  V_PERMS_TYPE VARCHAR2(30) := UPPER('&2');

  TYPE OBJ_T IS REF CURSOR;
  OBJ OBJ_T;

  TYPE I_T IS RECORD (
    POLICY_NAME        AUDIT_UNIFIED_POLICIES.POLICY_NAME%TYPE,
    AUDIT_CONDITION    AUDIT_UNIFIED_POLICIES.AUDIT_CONDITION%TYPE,
    CONDITION_EVAL_OPT AUDIT_UNIFIED_POLICIES.CONDITION_EVAL_OPT%TYPE,
    AUDIT_OPTION       AUDIT_UNIFIED_POLICIES.AUDIT_OPTION%TYPE,
    AUDIT_OPTION_TYPE  AUDIT_UNIFIED_POLICIES.AUDIT_OPTION_TYPE%TYPE,
    OBJECT_SCHEMA      AUDIT_UNIFIED_POLICIES.OBJECT_SCHEMA%TYPE,
    OBJECT_NAME        AUDIT_UNIFIED_POLICIES.OBJECT_NAME%TYPE,
    OBJECT_TYPE        AUDIT_UNIFIED_POLICIES.OBJECT_TYPE%TYPE,
    INHERITED          VARCHAR2(3),
    COMMON             AUDIT_UNIFIED_POLICIES.COMMON%TYPE,
    CON_ID             NUMBER
  );
  I I_T;

  FUNCTION QA (IN_VALUE IN VARCHAR2) RETURN VARCHAR2 AS
    V_ENCLOSURE VARCHAR2(1) := '"';
    V_SEPARATOR VARCHAR2(1) := ',';
    OUT_VALUE   VARCHAR2(4000);
  BEGIN
    IF IN_VALUE IS NOT NULL THEN
      OUT_VALUE := REPLACE(REPLACE(IN_VALUE,CHR(13),' '),CHR(10),' ');
      IF OUT_VALUE LIKE '%' || V_ENCLOSURE || '%' OR OUT_VALUE LIKE '%' || V_SEPARATOR || '%' THEN
        RETURN V_ENCLOSURE || REPLACE(OUT_VALUE,V_ENCLOSURE,V_ENCLOSURE || V_ENCLOSURE) || V_ENCLOSURE;
      ELSE
        RETURN OUT_VALUE;
      END IF;
    ELSE
      RETURN NULL;
    END IF;
  END;

BEGIN
  DBMS_OUTPUT.ENABLE(NULL);
  SELECT SUBSTR(VERSION,1,INSTR(VERSION,'.',1,4)-1),SUBSTR(VERSION,1,INSTR(VERSION,'.',1,1)-1) INTO V_ORA_VERSION,V_ORA_VER_MAJOR FROM SYS.V$INSTANCE;

  IF v_ora_version = '12.1.0.1' THEN
    V_SQL := q'[
   SELECT POLICY_NAME,
          AUDIT_CONDITION,
          CONDITION_EVAL_OPT,
          AUDIT_OPTION,
          AUDIT_OPTION_TYPE,
          OBJECT_SCHEMA,
          OBJECT_NAME,
          OBJECT_TYPE,
          NULL INHERITED,
          COMMON,
          CON_ID
   FROM   CDB$VIEW("AUDIT_UNIFIED_POLICIES")
   ORDER  BY 1,2,3,4]';
  ELSIF v_ora_version = '12.1.0.2' THEN
    V_SQL := q'[
   SELECT POLICY_NAME,
          AUDIT_CONDITION,
          CONDITION_EVAL_OPT,
          AUDIT_OPTION,
          AUDIT_OPTION_TYPE,
          OBJECT_SCHEMA,
          OBJECT_NAME,
          OBJECT_TYPE,
          NULL INHERITED,
          COMMON,
          CON_ID
   FROM   CONTAINERS(AUDIT_UNIFIED_POLICIES)
   ORDER  BY 1,2,3,4]';
  ELSIF v_ora_ver_major >= 12 THEN
    V_SQL := q'[
   SELECT POLICY_NAME,
          AUDIT_CONDITION,
          CONDITION_EVAL_OPT,
          AUDIT_OPTION,
          AUDIT_OPTION_TYPE,
          OBJECT_SCHEMA,
          OBJECT_NAME,
          OBJECT_TYPE,
          INHERITED,
          COMMON,
          CON_ID
   FROM   CONTAINERS(AUDIT_UNIFIED_POLICIES)
   ORDER  BY 1,2,3,4]';
  ELSE
    V_SQL := q'[
      SELECT NULL POLICY_NAME,
             NULL AUDIT_CONDITION,
             NULL CONDITION_EVAL_OPT,
             NULL AUDIT_OPTION,
             NULL AUDIT_OPTION_TYPE,
             NULL OBJECT_SCHEMA,
             NULL OBJECT_NAME,
             NULL OBJECT_TYPE,
             NULL INHERITED,
             NULL COMMON,
             NULL CON_ID
      FROM   DUAL
      WHERE  1=2]';
  END IF;

  OPEN OBJ FOR V_SQL;  

  CASE V_PERMS_TYPE
   WHEN 'AUDIT_UNIFIED_POLICIES' THEN
    LOOP
      FETCH OBJ INTO I;
      EXIT WHEN OBJ%NOTFOUND;
      DBMS_OUTPUT.PUT_LINE(
      QA(I.POLICY_NAME) || V_SEPARATOR ||
      QA(I.AUDIT_CONDITION) || V_SEPARATOR ||
      QA(I.CONDITION_EVAL_OPT) || V_SEPARATOR ||
      QA(I.AUDIT_OPTION) || V_SEPARATOR ||
      QA(I.AUDIT_OPTION_TYPE) || V_SEPARATOR ||
      QA(I.OBJECT_SCHEMA) || V_SEPARATOR ||
      QA(I.OBJECT_NAME) || V_SEPARATOR ||
      QA(I.OBJECT_TYPE) || V_SEPARATOR ||
      QA(I.INHERITED) || V_SEPARATOR ||
      QA(I.COMMON) || V_SEPARATOR ||
      QA(I.CON_ID));
    END LOOP;
   ELSE
    NULL;
  END CASE;

  CLOSE OBJ;
END;
/

SPOOL OFF